#? stdtmpl | standard
#import strutils
#proc listTemplate*(title: string, posts: seq[PostPreview], menus: seq[seq[MenuItem]], page, totalPages: int, urlSuffix = ".html", config: SiteConfig): string =
#  let prevUrl = if page == 2: title & urlSuffix else: title & "-" & $(page-1) & urlSuffix
#  let nextUrl = title & "-" & $(page+1) & urlSuffix
#  let isHomePage = title.toLowerAscii == "index"
#  let pageTitle = if totalPages > 1: title & " - Page " & $page else: title
#  let fullTitle = if isHomePage and config.siteTitle.len > 0 and config.siteDescription.len > 0: config.siteTitle & " - " & config.siteDescription elif isHomePage and config.siteTitle.len > 0: config.siteTitle elif config.siteTitle.len > 0: pageTitle & " - " & config.siteTitle else: pageTitle
#  let pageLinks = generatePageLinks(title, page, totalPages, urlSuffix)
#  let topMenu = if menus.len > 0: menus[0] else: @[]
#  let bottomMenu = if menus.len > 1: menus[^1] else: @[]
<!DOCTYPE html>
<html lang="en">
${renderHead(fullTitle, config)}
<body>
${renderSiteHeader(config)}
${renderTopNav(topMenu)}
  <main>
    <ul class="post-list">
#  for post in posts:
      <li>
        <article>
#    if post.tags.len > 0:
          <div class="tags">
#      for tag in post.tags:
            <a href="${tag.slug}.html">${tag.label}</a>
#      end for
          </div>
#    end if
          <time datetime="${formatDate(post.date, post.dateHasTime)}">${formatDate(post.date, post.dateHasTime)}</time>
          <div class="preview">
${post.preview}
          </div>
          <a href="${post.url}" class="read-more">Read more</a>
        </article>
      </li>
#  end for
    </ul>
#  if totalPages > 1:
    <nav class="pagination">
#    if page > 1:
      <a href="${prevUrl}" rel="prev">&laquo;</a>
#    else:
      <span class="disabled">&laquo;</span>
#    end if
#    for link in pageLinks:
#      if link.ellipsis:
      <span class="ellipsis">&hellip;</span>
#      elif link.current:
      <span class="current">${link.page}</span>
#      else:
      <a href="${link.url}">${link.page}</a>
#      end if
#    end for
#    if page < totalPages:
      <a href="${nextUrl}" rel="next">&raquo;</a>
#    else:
      <span class="disabled">&raquo;</span>
#    end if
    </nav>
#  end if
  </main>
${renderFooter(bottomMenu, menus.len > 1)}
</body>
</html>
